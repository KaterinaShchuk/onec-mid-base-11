
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//Доработка Щукина Е.Д. Добавление контактного лица
	ЭлементКонтактноеЛицо = Элементы.Вставить("ДЗ_КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо";
	//Конец добавления контактного лица  
	
	//Доработка Щукина Е.Д. Добавление группы
	ЭлементГруппаСкидка = Элементы.Вставить("ГруппаСкидка", Тип("ГруппаФормы"), ЭтаФорма, Элементы.Страницы);
	ЭлементГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	//Конец добавления группы
		
	//Доработка Щукина Е.Д. Добавление поля ввода скидки
	ЭлементСогласованнаяСкидка = Элементы.Вставить("ДЗ_СогласованнаяСкидка", Тип("ПолеФормы"), ЭлементГруппаСкидка);
	ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка";
	ЭлементСогласованнаяСкидка.Заголовок = "Согласованная скидка";
	ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ДЗ_СогласованнаяСкидкаПриИзменении");
	//Конец добавления поля ввода скидки
	
	//Доработка Щукина Е.Д. Добавление команды расчета скидки
	КомандаПересчитатьЦены = Команды.Добавить("ПересчитатьЦены");
	КомандаПересчитатьЦены.Заголовок = "Пересчитать цены";
	КомандаПересчитатьЦены.Действие = "ДЗ_ПересчитатьЦены";
	
	КнопкаКоманды = Элементы.Вставить("КомандаПересчитатьЦены", Тип("КнопкаФормы"), ЭлементГруппаСкидка);
	КнопкаКоманды.ИмяКоманды = "ПересчитатьЦены";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	//Конец добавления команды расчета скидки
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДЗ_СогласованнаяСкидкаПриИзменении(Элемент)
	
	//Доработка Щукина Е.Д. Пересчет сумм при изменении согласованной скидки
	Обещание = ВопросАсинх("Изменен процент скидки. Пересчитать цены с учетом измененной скидки?", РежимДиалогаВопрос.ДаНет,,,"Вопрос");	
	РезультатВопроса = Ждать Обещание;
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Для Каждого ТекущаяСтрока Из Объект.Товары Цикл
			РассчитатьСуммуСтроки(ТекущаяСтрока);
		КонецЦикла;
	
	    Для Каждого ТекущаяСтрока Из Объект.Услуги Цикл
			РассчитатьСуммуСтроки(ТекущаяСтрока);
		КонецЦикла;
		
	КонецЕсли;
	//Конец доработки пересчета сумм при изменении согласованной скидки

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//Расчет суммы на поддержке
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	
	//Доработка Щукина Е.Д. Расчет суммы строки с учетом скидки
	ТекущиеДанные.Сумма = (ТекущиеДанные.Цена * ТекущиеДанные.Количество) * (1 - Объект.ДЗ_СогласованнаяСкидка / 100);
	//Конец расчета
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область Доработки

&НаКлиенте
Процедура ДЗ_ПересчитатьЦены(Команда)
	
	//Доработка Щукина Е.Д. Пересчет цен с учетом скидки
	Для Каждого ТекущаяСтрока Из Объект.Товары Цикл
		РассчитатьСуммуСтроки(ТекущаяСтрока);
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(ТекущаяСтрока);
	КонецЦикла; 
	//Конец доработки пересчета цен с учетом скидки
	
КонецПроцедуры 

#КонецОбласти

